# Prefer the vendored zlib target before falling back to CMake's default module.
if(TARGET zlibstatic)
  if(NOT TARGET ZLIB::ZLIBSTATIC)
    add_library(ZLIB::ZLIBSTATIC ALIAS zlibstatic)
  endif()
  if(NOT TARGET ZLIB::ZLIB)
    add_library(ZLIB::ZLIB ALIAS zlibstatic)
  endif()

  get_target_property(_qtapng_zlib_inc zlibstatic INTERFACE_INCLUDE_DIRECTORIES)
  if(NOT _qtapng_zlib_inc)
    if(DEFINED zlib_SOURCE_DIR)
      list(APPEND _qtapng_zlib_inc "${zlib_SOURCE_DIR}")
    endif()
    if(DEFINED zlib_BINARY_DIR)
      list(APPEND _qtapng_zlib_inc "${zlib_BINARY_DIR}")
    endif()
  endif()
  if(_qtapng_zlib_inc)
    set(ZLIB_INCLUDE_DIRS ${_qtapng_zlib_inc})
    list(GET ZLIB_INCLUDE_DIRS 0 ZLIB_INCLUDE_DIR)
  endif()

  set(ZLIB_LIBRARIES ZLIB::ZLIB)
  set(ZLIB_LIBRARY ZLIB::ZLIB)
  if(DEFINED zlib_VERSION)
    set(ZLIB_VERSION_STRING "${zlib_VERSION}")
  endif()
  set(ZLIB_FOUND TRUE)
  return()
endif()

include("${CMAKE_ROOT}/Modules/FindZLIB.cmake")
